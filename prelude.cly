; Atoms
(def {Nil} {})
(def {True} 1)
(def {False} 0)

; Function Definitions
(def {fun} (\ {f a} {
    def (head f) (\ (tail f) a)
}))

; Unpack List Function
(fun {unpack f l} {
    eval (join (list f) l)
})

; Pack List Function
(fun {pack f & xs} {f xs})

; Curried and Uncurried calling
(def {curry} unpack)
(def {uncurry} pack)

; List Algorithms

;; First, Second and Third items in a list
(fun {fst l} { eval (head l) })
(fun {snd l} { eval (head (tail l)) })
(fun {trd l} { eval (head (tail (tail l))) })

;; Length of List
(fun {len l} {
    if (== l Nil)
        {0}
        {+ 1 (len (tail l))}
})

;; Nth Item
(fun {nth n l} {
    if (== n 0)
        {fst l}
        {nth (- n 1) (tail l)}
})

;; Last Item
(fun {last l} {nth (- (len l) 1) l})

;; Take N Items
(fun {take n l} {
    if (== n 0)
        {Nil}
        {join (head l) (take (- n 1) (tail l))}
})

;; Drop N Items
(fun {drop n l} {
    if (== n 0)
        {l}
        {drop (- n 1) (tail l)}
})

;; Split at Nth
(fun {split n l} {list (take n l) (drop n l)})

;; Element exist in list
(fun {elem-in x l} {
    if (== l Nil)
        {False}
        {if (== x (fst l)) {True} {elem x (tail l)}}
})

;; Map
(fun {map f l} {
    if (== l Nil)
        {Nil}
        {join (list (f (fst l))) (map f (tail l))}
})

;; Filter
(fun {filter f l} {
    if (== l Nil)
        {Nil}
        {join (if (f (fst l)) {head l} {Nil}) (filter f (tail l))}
})

;; Fold Left
(fun {foldl f z l} {
    if (== l Nil)
        {z}
        {foldl f (f z (fst l)) (tail l)}
})

;; Sum and Product
(fun {sum l} {foldl + 0 l})
(fun {product l} {foldl * 1 l})

; Statement Expression

;; Select
(fun {select & cs} {
    if (== cs Nil)
        {error "No Selection Found"}
        {if (fst (fst cs)) {snd (fst cs)} {unpack select (tail cs)}}
})

;; Otherwise
(def {otherwise} True)

;;; eg.

;;; Print Day of Month suffix
;;; (fun {month-day-suffix i} {
;;;     select
;;;         {(== i 0)  "st"}
;;;         {(== i 1)  "nd"}
;;;         {(== i 3)  "rd"}
;;;         {otherwise "th"}
;;; })

;; Case
(fun {case x & cs} {
    if (== cs Nil)
        {error "No Case Found"}
        {if (== x (fst (fst cs))) {snd (fst cs)} {
            unpack case (join (list x) (tail cs))}}
})

;;; eg.

;;; Day of the week
;;; (fun {day-name x} {
;;;     case x
;;;         {0 "Monday"}
;;;         {1 "Tuesday"}
;;;         {2 "Wednesday"}
;;;         {3 "Thursday"}
;;;         {4 "Friday"}
;;;         {5 "Saturday"}
;;;         {6 "Sunday"}
;;; })

; do block
(fun {do & l} {
    if (== l Nil)
        {Nil}
        {last l}
})

; Open new scope
(fun {let a} {
    ((\ {_} a) ())
})

; Logical Functions
(fun {not x} {- 1 x})
(fun {or x y} {+ x y})
(fun {and x y} {* x y})

; Utility Functions
(fun {flip f a b} {f b a})
(fun {ghost & xs} {eval xs})
(fun {compose f g x} {f (g x)})
